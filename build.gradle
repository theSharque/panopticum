plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.6.1"
}

version = "5.0.1"
group = "com.panopticum"

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == "ch.qos.logback") {
                details.useVersion("1.5.25")
                details.because("CVE fixes: 2024-12798, 2025-11226, 2024-12801, 2026-1225")
            }
            if (details.requested.group == "com.microsoft.sqlserver" && details.requested.name == "mssql-jdbc") {
                details.useVersion("12.10.2.jre11")
                details.because("CVE fix: 2025-59250")
            }
        }
    }
}

dependencies {
    implementation(platform("io.micronaut.platform:micronaut-platform:4.10.7"))
    annotationProcessor("org.projectlombok:lombok")
    annotationProcessor("io.micronaut:micronaut-core-processor")
    compileOnly("io.micronaut:micronaut-core-processor")
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-http-server-netty")
    implementation("io.micronaut.security:micronaut-security")
    implementation("io.micronaut.views:micronaut-views-thymeleaf")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation("io.micronaut.flyway:micronaut-flyway")
    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation("io.micronaut.validation:micronaut-validation")
    compileOnly("org.projectlombok:lombok")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("ch.qos.logback:logback-core")
    runtimeOnly("com.h2database:h2")
    runtimeOnly("org.postgresql:postgresql")
    runtimeOnly("com.mysql:mysql-connector-j")
    runtimeOnly("com.microsoft.sqlserver:mssql-jdbc")
    runtimeOnly("com.oracle.database.jdbc:ojdbc11")
    implementation("org.mongodb:mongodb-driver-sync")
    implementation("io.lettuce:lettuce-core:6.5.4.RELEASE")
    runtimeOnly("com.clickhouse:clickhouse-jdbc:0.9.6:all")
    implementation("com.datastax.oss:java-driver-core:4.17.0")
    runtimeOnly("org.yaml:snakeyaml")
}

application {
    mainClass.set("com.panopticum.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(false)
        annotations("com.panopticum.*")
    }
}

shadowJar {
    mergeServiceFiles()
}
